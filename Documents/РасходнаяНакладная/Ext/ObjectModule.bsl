 
 
 Процедура ПроведениеОУ(Отказ)
	 
	 
	 
	 Движения.ОстаткиНоменклатуры.Записывать = Истина;
	 Для Каждого ТекСтрокаСписокНоменклатуры Из СписокНоменклатуры Цикл
		 Движение = Движения.ОстаткиНоменклатуры.Добавить();
		 Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		 Движение.Период = Дата;
		 Движение.Номенклатура = ТекСтрокаСписокНоменклатуры.Номенклатура;
		 Движение.Склад = Склад;
		 Движение.Количество = ТекСтрокаСписокНоменклатуры.Количество;
	 КонецЦикла;
	 
	 Движения.Записать();
	 
	 Запрос = Новый Запрос;
	 Запрос.Текст = 
	 "ВЫБРАТЬ
	 |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	 |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	 |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.Представление КАК НоменклатураПредставление
	 |ПОМЕСТИТЬ ВТ_ТЧ
	 |ИЗ
	 |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	 |ГДЕ
	 |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	 |	И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = &ВидНоменклатуры
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ВТ_ТЧ.Номенклатура КАК Номенклатура,
	 |	ВТ_ТЧ.Количество КАК Количество,
	 |	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	 |	ВТ_ТЧ.НоменклатураПредставление КАК НоменклатураПредставление
	 |ИЗ
	 |	ВТ_ТЧ КАК ВТ_ТЧ
	 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
	 |				&МоментВремени,
	 |				Склад = &Склад
	 |					И Номенклатура В
	 |						(ВЫБРАТЬ
	 |							ВТ_ТЧ.Номенклатура КАК Номенклатура
	 |						ИЗ
	 |							ВТ_ТЧ КАК ВТ_ТЧ)) КАК Остатки
	 |		ПО ВТ_ТЧ.Номенклатура = Остатки.Номенклатура";
	 
	 Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	 Запрос.УстановитьПараметр("ВидНоменклатуры", Перечисления.ВидыНоменклатуры.Товар);
	 Запрос.УстановитьПараметр("Склад", Склад);
	 Запрос.УстановитьПараметр("Ссылка", Ссылка);
	 
	 РезультатЗапроса = Запрос.Выполнить();
	 
	 Выборка = РезультатЗапроса.Выбрать();
	 
	 Пока Выборка.Следующий() Цикл
		 Если Выборка.КоличествоОстаток < 0 Тогда
			 Отказ = Истина;
			 Сообщение = Новый СообщениеПользователю;
			 Сообщение.Текст = "Недостаточно товаров " + Выборка.НоменклатураПредставление + " в количестве " + -Выборка.КоличествоОстаток;
			 Сообщение.Сообщить();
		 КонецЕсли;
	 КонецЦикла;
	 
	 Если Отказ Тогда
		 
		 Возврат
		 
	 КонецЕсли;
	 
	 Блокировка = Новый БлокировкаДанных; 
	 Элемент = Блокировка.Добавить("РегистрНакопления.ОстаткиПартийТоваров");  
	 Элемент.Режим = РежимБлокировкиДанных.Исключительный;  
	 Элемент.ИсточникДанных = СписокНоменклатуры; 
	 Элемент.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");  
	 Блокировка.Заблокировать();
	 
	 МетодСписания = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата).МетодСписания;
	 
	 Если МетодСписания = Перечисления.УчетнаяПолитика.ФИФО Тогда
		 МС = " ВОЗР";
	 Иначе
		 МС = " УБЫВ";
	 КонецЕсли;
	 
	 Запрос = Новый Запрос;
	 Запрос.Текст = 
	 "ВЫБРАТЬ
	 |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	 |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	 |	РасходнаяНакладнаяСписокНоменклатуры.Цена КАК Цена
	 |ПОМЕСТИТЬ ВТ_ТЧ
	 |ИЗ
	 |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	 |ГДЕ
	 |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	 |	И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = &ВидНоменклатуры
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	 |	РасходнаяНакладнаяСписокНоменклатуры.Цена
	 |
	 |ИНДЕКСИРОВАТЬ ПО
	 |	Номенклатура
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ВТ_ТЧ.Номенклатура КАК Номенклатура,
	 |	ОстаткиПартий.Партия КАК Партия,
	 |	ВТ_ТЧ.Количество КАК Количество,
	 |	ВТ_ТЧ.Цена КАК Цена,
	 |	ЕСТЬNULL(ОстаткиПартий.КоличествоОстаток, 0) КАК КоличествоОстаток,
	 |	ЕСТЬNULL(ОстаткиПартий.СуммаОстаток, 0) КАК СуммаОстаток
	 |ИЗ
	 |	ВТ_ТЧ КАК ВТ_ТЧ
	 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиПартийТоваров.Остатки(
	 |				&МоментВремени,
	 |				Номенклатура В
	 |					(ВЫБРАТЬ
	 |						ВТ_ТЧ.Номенклатура КАК Номенклатура
	 |					ИЗ
	 |						ВТ_ТЧ КАК ВТ_ТЧ)) КАК ОстаткиПартий
	 |		ПО ВТ_ТЧ.Номенклатура = ОстаткиПартий.Номенклатура
	 |
	 |УПОРЯДОЧИТЬ ПО
	 |	Партия "+МС+"
	 |ИТОГИ
	 |	МАКСИМУМ(Количество)
	 |ПО
	 |	Номенклатура";
	 
	 Запрос.УстановитьПараметр("ВидНоменклатуры", перечисления.ВидыНоменклатуры.Товар);
	 Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	 Запрос.УстановитьПараметр("Ссылка", Ссылка);
	 
	 РезультатЗапроса = Запрос.Выполнить();
	 
	 ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	 
	 Движения.ОстаткиПартийТоваров.Записывать = Истина;
	 
	 Пока ВыборкаНоменклатура.Следующий() Цикл
		 НадоСписать = ВыборкаНоменклатура.Количество;
		 
		 Выборка = ВыборкаНоменклатура.Выбрать();
		 
		 Пока Выборка.Следующий() и НадоСписать > 0 Цикл
			 Списать = МИН(НадоСписать, Выборка.КоличествоОстаток);
			 Движение = Движения.ОстаткиПартийТоваров.ДобавитьРасход();
			 Движение.Период = Дата;
			 Движение.Номенклатура = Выборка.Номенклатура;
			 Движение.Партия = Выборка.Партия;
			 Движение.Количество = Списать;
			 Если НадоСписать = Выборка.КоличествоОстаток Тогда
				 Себестоимость = Выборка.СуммаОстаток;
			 Иначе
				 Себестоимость = Выборка.СуммаОстаток / Выборка.КоличествоОстаток * Списать;
			 КонецЕсли;
			 Движение.Сумма = Себестоимость;
			 НадоСписать = НадоСписать - Списать;	
		 КонецЦикла;
	 КонецЦикла;
	 
	 
	 
 КонецПроцедуры
 
 Процедура ПроведениеБУ(Отказ)
	 
	 Движения.Управленческий.Записывать = Истина;
	 
	 Запрос = Новый Запрос;
	 Запрос.Текст = 
	 "ВЫБРАТЬ
	 |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
	 |	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
	 |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.Представление КАК НоменклатураПредставление,
	 |	РасходнаяНакладнаяСписокНоменклатуры.Сумма КАК Сумма
	 |ПОМЕСТИТЬ ВТ_ТЧ
	 |ИЗ
	 |	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
	 |ГДЕ
	 |	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
	 |	И РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.ВидНоменклатуры = &ВидНоменклатуры
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура,
	 |	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура.Представление,
	 |	РасходнаяНакладнаяСписокНоменклатуры.Сумма
	 |
	 |ИНДЕКСИРОВАТЬ ПО
	 |	Номенклатура
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ВТ_ТЧ.Номенклатура КАК Номенклатура,
	 |	ОстаткиНомСроки.Субконто2 КАК СрокГодности,
	 |	ВТ_ТЧ.Количество КАК Количество,
	 |	ЕСТЬNULL(ОстаткиНомСроки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	 |	ЕСТЬNULL(ОстаткиНом.СуммаОстаток, 0) КАК СуммаОстаток,
	 |	ВТ_ТЧ.НоменклатураПредставление КАК НоменклатураПредставление,
	 |	ВТ_ТЧ.Сумма КАК Сумма
	 |ИЗ
	 |	ВТ_ТЧ КАК ВТ_ТЧ
	 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
	 |				&МоментВремени,
	 |				Счет = &СчетТовары,
	 |				&СубконтоНомСроки,
	 |				Субконто1 В
	 |					(ВЫБРАТЬ
	 |						ВТ_ТЧ.Номенклатура КАК Номенклатура
	 |					ИЗ
	 |						ВТ_ТЧ КАК ВТ_ТЧ)) КАК ОстаткиНомСроки
	 |		ПО ВТ_ТЧ.Номенклатура = ОстаткиНомСроки.Субконто1
	 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
	 |				&МоментВремени,
	 |				Счет = &СчетТовары,
	 |				&СубконтоНом,
	 |				Субконто1 В
	 |					(ВЫБРАТЬ
	 |						ВТ_ТЧ.Номенклатура КАК Номенклатура
	 |					ИЗ
	 |						ВТ_ТЧ КАК ВТ_ТЧ)) КАК ОстаткиНом
	 |		ПО ВТ_ТЧ.Номенклатура = ОстаткиНом.Субконто1
	 |ИТОГИ
	 |	МАКСИМУМ(Количество),
	 |	СУММА(КоличествоОстаток),
	 |	СУММА(СуммаОстаток),
	 |	МАКСИМУМ(НоменклатураПредставление),
	 |	МАКСИМУМ(Сумма)
	 |ПО
	 |	Номенклатура";
	 
	 Запрос.УстановитьПараметр("ВидНоменклатуры", Перечисления.ВидыНоменклатуры.Товар);
	 Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	 Запрос.УстановитьПараметр("Ссылка", Ссылка);
	 
	 СубконтоНоменклатура = Новый массив;
	 СубконтоНоменклатура.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
	 
	 Запрос.УстановитьПараметр("СубконтоНом", СубконтоНоменклатура);
	 
	 СубконтоНоменклатураСрок = Новый Массив;
	 СубконтоНоменклатураСрок.Добавить(ПланыВидовХарактеристик.ВидыСубконто.Номенклатура);
	 СубконтоНоменклатураСрок.Добавить(ПланыВидовХарактеристик.ВидыСубконто.СрокГодности);
	 
	 Запрос.УстановитьПараметр("СубконтоНомСроки", СубконтоНоменклатураСрок);
	 Запрос.УстановитьПараметр("СчетТовары", ПланыСчетов.Управленческий.Товары);
	 
	 РезультатЗапроса = Запрос.Выполнить();
	 
	 ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	 
	 Пока ВыборкаНоменклатура.Следующий() Цикл
		 Нехватка = ВыборкаНоменклатура.КоличествоОстаток - ВыборкаНоменклатура.Количество;
		 
		 Если Нехватка < 0 Тогда
			 Отказ = Истина;
			 Сообщение = Новый СообщениеПользователю;
			 Сообщение.Текст = "Недостаточно товара " + ВыборкаНоменклатура.НоменклатураПредставление + " в количестве " + -Нехватка + " шт.";
			 Сообщение.Сообщить();
		 КонецЕсли;
		 
		 Если Отказ Тогда
			 Продолжить;
		 КонецЕсли;
		 
		 НадоСписать = ВыборкаНоменклатура.Количество;
		 
		 Выборка = ВыборкаНоменклатура.Выбрать();
		 
		 Пока Выборка.Следующий() и НадоСписать > 0 Цикл
			 Списать = МИН(Выборка.Количество, НадоСписать);
			 Движение = Движения.Управленческий.ДобавитьКредит();
			 Движение.Период = Дата;
			 Движение.СчетКт = ПланыСчетов.Управленческий.Товары;
			 Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
			 Движение.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.СрокГодности] = Выборка.СрокГодности;
			 Движение.КоличествоКт = списать;
			 Движение.Сумма = Выборка.Суммаостаток / Выборка.КоличествоОстаток * Списать;
			 НадоСписать = НадоСписать - Списать;
			 
			 Движение.СчетДт = ПланыСчетов.Управленческий.ПрибылиУбытки;
		 КонецЦикла;
		 
		 //Дт «Покупатели» - Кт «Прибыли и убытки» на сумму в продажных ценах.
		 Движение = Движения.Управленческий.ДобавитьДебет();
		 Движение.Период = Дата;
		 Движение.СчетДт = ПланыСчетов.Управленческий.Покупатели;
		 Движение.СчетКт = ПланыСчетов.Управленческий.ПрибылиУбытки;
		 Движение.Сумма = ВыборкаНоменклатура.Сумма;
		 
	 КонецЦикла;
	 
	 
	 
 КонецПроцедуры
 
 
 Процедура ОбработкаПроведения(Отказ, Режим)
	 ПроведениеОУ(Отказ);
	 ПроведениеБУ(Отказ);	 
 КонецПроцедуры
